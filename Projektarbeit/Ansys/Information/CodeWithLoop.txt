!========================================================
! Wandscheibe mit ï¿½ffnungen
! Einheiten: [kN],[m]
!========================================================


/nerr,0

*do,j,1,10,1
/com,
*enddo
/COM *** 5.2.2 ANSYS APDL ***

finish
/clear,start


*ask,meshsize,Anfangsnetzgroesse,0.5
*ask,verfein,Verfeinerungsfaktor,2.0
*ask,steps,Verfeinerungsschritte,3

*DIM,results,TABLE,steps,6
*TAXIS,results(1,1),1,1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,
*TAXIS,results(1,1),2,1.,2.,3.,4.,5.,6.


! *** loop to restart the calculation ***
!------------------------------------------------
*do,j,1,steps,1


/com,  %j%  %meshsize%
results(j,1) = meshsize



parsav,all
finish
/clear,start
parres



/prep7

blc4,0,0,10,10
blc4,5,0,5,5

asba,1,2

ET,1,PLANE182
KEYOPT,1,1,0
KEYOPT,1,3,3
KEYOPT,1,6,0

r,1,0.2

mp,ex,1,30000000
mp,prxy,1,0.2

dl,9,,all
sfl,3,pres,250

esize,meshsize
amesh,all
cpintf,all

/solu
solve

/post1


! maximale Verschiebung bei 10;10
allsel
nsel,s,loc,x,10
nsel,r,loc,y,10
*get,u,NODE,NDNEXT(0),u,y
results(j,3) = u



PATH,hori,2,30,5000
PPATH,1,0,0,3,,0,
PPATH,2,0,5,3,,0,
PDEF,sy,S,Y,NOAVG
PCALC,INTG,intsy,SY,XG,1,
PAGET,array_hori,TABL
syh=array_hori(5001,6)
*DEL,array_hori

results(j,4)=syh


allsel
*get,nn,NODE,0,count
results(j,2)=nn*2


!! Netzgroesse anpassen
meshsize = meshsize/verfein


*enddo



*do,j,1,steps,1
/com,  %j%   %results(j,1)%   %results(j,2)%   %results(j,3)%   %results(j,4)%   %results(j,5)%   %results(j,6)%
*enddo




/gcol,1,max_u
*VPLOT,results(1,2),results(1,3)
*ask,weiter,Weiter...,1


/gcol,1,GG
*VPLOT,results(1,2),results(1,4)
*ask,weiter,Weiter...,1



*do,j,1,steps,1
/com,  %j%   %results(j,1)%   %results(j,2)%   %results(j,3)%   %results(j,4)%   %results(j,5)%   %results(j,6)%
*enddo

! *** exit the programm ***
!------------------------------------------------
finish
!/exit